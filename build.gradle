buildscript {
    ext {
        kotlinVersion = '1.2.50'
        springBootVersion = '2.0.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

plugins {
    id "org.jetbrains.kotlin.plugin.jpa" version "1.2.50"
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6"
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'kotlin-jpa'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "io.gitlab.arturbosch.detekt"

group = 'de.schreib.handball'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
}


dependencies {
    // Production Monitoring
    runtime('org.springframework.boot:spring-boot-starter-actuator')

    // Spring developement tools
    compile('org.springframework.boot:spring-boot-devtools')

    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    runtime('org.postgresql:postgresql')

    // Mapping java8 Date types to database types
    compile('org.hibernate:hibernate-java8')

    // Database migration
    compile('org.liquibase:liquibase-core')

    // Serializer for java8 Date types
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')

    // Mail
    //compile('org.springframework.boot:spring-boot-starter-mail')

    // Kotlin coroutine hilfsmethoden
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.23.4'


    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    testCompile "org.mockito:mockito-core:2.+"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-inline:2.16.0'
    testCompile "com.opentable.components:otj-pg-embedded:$pgEmbeddedVersion"

}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

task dockerRest(type: Copy) {
    from 'build/libs'
    include "handballtag-${version}.jar"

    into "docker/rest/deploy/"
    rename '.*', 'handballtag-service.jar'
}

detekt {
    version = "1.0.0.RC6"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

def DEV_GROUP = 'Dev Environment'


task databaseUp(type: Exec) {
    group = DEV_GROUP
    commandLine './start-dev.sh'
}

task databaseDown(type: Exec) {
    group = DEV_GROUP
    commandLine './stop-dev.sh'
}
